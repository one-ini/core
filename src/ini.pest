ini = { SOI ~ bom? ~ (blank | pair | comment)* ~ section* }

// lines
blank = _{ "" ~ NEWLINE }
pair = { key ~ "=" ~ value ~ eol }
comment = { indicator ~ value? ~ eol }

// sections
section = { header ~ eol ~ body }
body = { (blank | pair | comment)* }

// tokens
// a name must end with a closing bracket followed by EOL
header = @{ "[" ~ (!("]" ~ (char | eol)) ~ char)+ ~ "]" }
// a key may not begin with an opening bracket and may not contain an equal sign
key = @{ (!("=" | "[") ~ char) ~ (!"=" ~ char)* }
value = @{ char+ }

// atomic primitives
bom = _{ "\u{feff}" }
indicator = { "#" | ";" }
char = { !eol ~ ANY }
eol = _{ NEWLINE | EOI }

WHITESPACE = _{ " " | "\t" }
